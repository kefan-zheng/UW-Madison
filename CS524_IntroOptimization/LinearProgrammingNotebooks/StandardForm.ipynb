{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Converting to standard form -- a step-by-step example\n",
    "\n",
    "Convert the following problem to standard form:\n",
    "\n",
    "$\\begin{align}\n",
    "\\min_{x_1,x_2} & \\ x_1 + x_2\\\\\n",
    "\\text{s.t.} & \\ 5ğ‘¥_1âˆ’3ğ‘¥_2 = 7\\\\\n",
    "& 2ğ‘¥_1+ğ‘¥_2â‰¥2\\\\\n",
    "& 1â‰¤ğ‘¥_2â‰¤4\\\\\n",
    "& ğ‘¥_1 \\ \\text{free}\n",
    "\\end{align}$\n",
    "\n",
    "A good way to check that you've done it correctly is by solving both the original and the standard form models in Julia. If your model is correct, you should get the same answer.\n",
    "\n",
    "## Original Model"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using JuMP, Clp\n",
    "\n",
    "m = Model(Clp.Optimizer)\n",
    "\n",
    "@variable(m, x1) # x1 is free\n",
    "@variable(m, 1 <= x2 <= 4 ) # x2 is bounded above and below\n",
    "\n",
    "# an inequality constraint and an equality constraint\n",
    "@constraint(m, 5*x1 - 3*x2 == 7 ) \n",
    "@constraint(m, 2*x1 + x2 >= 2 )\n",
    "\n",
    "# objective is to minimize x1 + x2\n",
    "@objective(m, Min, x1 + x2 )\n",
    "\n",
    "# solve the problem\n",
    "optimize!(m)\n",
    "\n",
    "# display the model\n",
    "println(m)\n",
    "println()\n",
    "println(\"x1 = \", value(x1) )\n",
    "println(\"x2 = \", value(x2) )\n",
    "println(\"objective = \", objective_value(m) )"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Converting to Standard Form\n",
    "\n",
    "When we convert it, the model should look like this:\n",
    "$\\begin{align}\n",
    "\\underset{x}{\\max} \\ & c^Tx & \\\\\n",
    "\\text{s.t.} \\ & Ax \\leq b &\\\\\n",
    "& x \\geq 0 &\n",
    "\\end{align}$\n",
    " \n",
    "\n",
    "* We bound $x_1$ by replacing it with $x_1 = u - v, \\ u, v \\geq 0$:\n",
    "\n",
    "\n",
    "$\\begin{align}\n",
    "\\underset{u,v,x_2}{\\min} \\ & (u-v) + x_2 & \\\\\n",
    "\\text{s.t.} \\ & 5(u-v) - 3x_2 = 7 &\\\\\n",
    "& 2(u-v) + x_2 \\geq 2 &\\\\\n",
    "& 1 \\leq x_2 \\leq 4 & \\\\\n",
    "& u, v \\geq 0&\n",
    "\\end{align}$\n",
    "\n",
    "\n",
    "* We shift the bounds on $x_2$ by using $w = x_2 - 1$:\n",
    "\n",
    "$\\begin{align}\n",
    "\\underset{u,v,w}{\\min} \\ & (u-v) + (w+1) & \\\\\n",
    "\\text{s.t.} \\ & 5(u-v) - 3(w+1) = 7 &\\\\\n",
    "& 2(u-v) + (w+1) \\geq 2 &\\\\\n",
    "& (w+1) \\leq 4 & \\\\\n",
    "& u, v, w \\geq 0&\n",
    "\\end{align}$\n",
    "\n",
    "* Rearrange so all constants are on the right-hand-side:\n",
    "\n",
    "$\\begin{align}\n",
    "\\underset{u,v,w}{\\min} \\ & u - v + w + 1 & \\\\\n",
    "\\text{s.t.} \\ & 5u- 5v - 3w = 10 &\\\\\n",
    "& 2u - 2v + w \\geq 1 &\\\\\n",
    "& w \\leq 3 & \\\\\n",
    "& u, v, w \\geq 0&\n",
    "\\end{align}$\n",
    "\n",
    "* We turn the min into a max:\n",
    "\n",
    "$\\begin{align}\n",
    "\\underset{u,v,w}{-\\max}\\ & -u + v - w - 1 & \\\\\n",
    "\\text{s.t.} \\ & 5u- 5v - 3w = 10 &\\\\\n",
    "& 2u - 2v + w \\geq 1 &\\\\\n",
    "& w \\leq 3 & \\\\\n",
    "& u, v, w \\geq 0&\n",
    "\\end{align}$\n",
    "\n",
    "* Flip $\\geq$ inequality and replace equality with two inequalities:\n",
    "\n",
    "$\\begin{align}\n",
    "\\underset{u,v,w}{-\\max}\\ & -u + v - w - 1 & \\\\\n",
    "\\text{s.t.} \\ & 5u- 5v - 3w \\leq 10 &\\\\\n",
    "& 5u- 5v - 3w \\geq 10 &\\\\\n",
    "& -2u + 2v - w \\leq -1 &\\\\\n",
    "& w \\leq 3 & \\\\\n",
    "& u, v, w \\geq 0&\n",
    "\\end{align}$\n",
    "\n",
    "\n",
    "* Finally, flip the newly created $\\geq$ inequality and we're done!\n",
    "\n",
    "$\\begin{align}\n",
    "\\underset{u,v,w}{-\\max}\\ & -u + v - w - 1 & \\\\\n",
    "\\text{s.t.} \\ & 5u- 5v - 3w \\leq 10 &\\\\\n",
    "& -5u + 5v + 3w \\leq -10 &\\\\\n",
    "& -2u + 2v - w \\leq -1 &\\\\\n",
    "& w \\leq 3 & \\\\\n",
    "& u, v, w \\geq 0&\n",
    "\\end{align}$\n",
    "\n",
    "\n",
    "Verify correctness by solving the new model in Julia:\n",
    "\n",
    "## Standard Form Model"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using JuMP, Clp\n",
    "\n",
    "m = Model(Clp.Optimizer)\n",
    "\n",
    "# three nonnegative variables\n",
    "@variable(m, u >= 0 )\n",
    "@variable(m, v >= 0 )\n",
    "@variable(m, w >= 0 )\n",
    "\n",
    "# four less than or equal to constraints\n",
    "@constraint(m, 5*u - 5*v - 3*w <= 10 )\n",
    "@constraint(m, -5*u + 5*v + 3*w <= -10 )\n",
    "@constraint(m, -2*u + 2*v - w <= -1 )\n",
    "@constraint(m, w <= 3 )\n",
    "\n",
    "# maximize the objective\n",
    "@objective(m, Max, -u + v - w - 1 )\n",
    "\n",
    "# solve the standard form model\n",
    "optimize!(m)\n",
    "\n",
    "# display the model and solution\n",
    "println(m)\n",
    "println()\n",
    "# remember to convert back to x1 and x2!\n",
    "println(\"x1 = \", value(u-v) )\n",
    "println(\"x2 = \", value(w+1) )\n",
    "# remember min f(x) = -max -f(x), so we need to report the negative of our objective value\n",
    "println(\"objective = \", -objective_value(m) )"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.10.0",
   "language": "julia",
   "name": "julia-1.10"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
